apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "0.2"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // Disable the built-in NDK task (this is too rigid for our purposes).
    sourceSets.main.jni.srcDirs = []

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn customNdkBuild
    }
}

def siteUrl = 'https://github.com/ndahlquist/pngquant-android'
def gitUrl = 'https://github.com/ndahlquist/pngquant-android.git'
group = "com.ndahlquist"

def execute(cmd) {
    def proc = cmd.execute()
    proc.waitFor()

    def buffer = new StringBuffer()
    proc.consumeProcessErrorStream(buffer)

    print proc.text
    print buffer.toString()

    if (proc.exitValue()) {
        throw new RuntimeException("Failed to execute command " + cmd)
    }
}


task customNdkBuild << {
    execute("pngquant-android/build_native_libs.sh")
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                name 'Pngquant with basic Java bindings for Android.'
                url siteUrl

                licenses {
                    license {
                        name 'BSD Simplified'
                    }
                }
                developers {
                    developer {
                        id 'ndahlquist'
                        name 'Nic Dahlquist'
                        email 'nic@snapchat.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "pngquant-android"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["BSD Simplified"]
        publish = true
    }
}
